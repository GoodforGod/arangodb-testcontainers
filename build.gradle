plugins {
    id "jacoco"
    id "java-library"
    id "maven-publish"

    id "org.sonarqube" version "4.3.0.3225"
    id "com.diffplug.spotless" version "6.19.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = groupId
version = artifactVersion

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    api "org.testcontainers:testcontainers:1.17.6"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.3"
    testRuntimeOnly "ch.qos.logback:logback-classic:1.4.7"

    testImplementation "org.testcontainers:junit-jupiter:1.17.6"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.slf4j:slf4j-api:1.7.36"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.required = false
        junitXml.required = false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "ArangoDB TestContainers"
                url = "https://github.com/GoodforGod/arangodb-testcontainer"
                description = "ArangoDB TestContainers implementation with startup strategy and cluster containers."

                license {
                    name = "MIT License"
                    url = "https://github.com/GoodforGod/arangodb-testcontainer/blob/master/LICENSE"
                    distribution = "repo"
                }

                developer {
                    id = "GoodforGod"
                    name = "Anton Kurako"
                    email = "goodforgod.dev@gmail.com"
                    url = "https://github.com/GoodforGod"
                }

                scm {
                    connection = "scm:git:git://github.com/GoodforGod/arangodb-testcontainer.git"
                    developerConnection = "scm:git:ssh://GoodforGod/arangodb-testcontainer.git"
                    url = "https://github.com/GoodforGod/arangodb-testcontainer/tree/master"
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("OSS_USERNAME")
                password System.getenv("OSS_PASSWORD")
            }
        }
    }
}

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse("4.21").configFile("${rootDir}/config/codestyle.xml")
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "goodforgod"
        property "sonar.projectKey", "GoodforGod_arangodb-testcontainer"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.required = true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: "signing"
    signing {
        sign publishing.publications.mavenJava
    }
}
