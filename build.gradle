plugins {
    id "jacoco"
    id "java-library"
    id "maven-publish"

    id "org.sonarqube" version "6.3.1.5724"
    id "com.diffplug.spotless" version "6.19.0"
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = groupId
var ver = System.getenv().getOrDefault("RELEASE_VERSION", artifactVersion)
version = ver.startsWith("v") ? ver.substring(1) : ver

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    api "org.testcontainers:testcontainers:1.21.3"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.11.4"
    testRuntimeOnly "ch.qos.logback:logback-classic:1.5.18"

    testImplementation "org.testcontainers:junit-jupiter:1.21.3"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.11.4"
    testImplementation "org.slf4j:slf4j-api:1.7.36"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.required = false
        junitXml.required = false
    }
}

nexusPublishing {
    packageGroup = groupId
    repositories {
        sonatype {
            username = System.getenv("OSS_USERNAME")
            password = System.getenv("OSS_PASSWORD")
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "ArangoDB TestContainers"
                url = "https://github.com/GoodforGod/arangodb-testcontainer"
                description = "ArangoDB TestContainers implementation with startup strategy and cluster containers."

                license {
                    name = "MIT License"
                    url = "https://github.com/GoodforGod/arangodb-testcontainer/blob/master/LICENSE"
                    distribution = "repo"
                }

                developer {
                    id = "GoodforGod"
                    name = "Anton Kurako"
                    email = "goodforgod.dev@gmail.com"
                    url = "https://github.com/GoodforGod"
                }

                scm {
                    connection = "scm:git:git://github.com/GoodforGod/arangodb-testcontainer.git"
                    developerConnection = "scm:git:ssh://GoodforGod/arangodb-testcontainer.git"
                    url = "https://github.com/GoodforGod/arangodb-testcontainer/tree/master"
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/"
            def snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("OSS_USERNAME")
                password System.getenv("OSS_PASSWORD")
            }
        }
        if (!version.endsWith("SNAPSHOT")) {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/GoodforGod/arangodb-testcontainers"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse("4.21").configFile("${rootDir}/config/codestyle.xml")
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "goodforgod"
        property "sonar.projectKey", "GoodforGod_arangodb-testcontainers"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.required = true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

if (project.hasProperty("signingKey")) {
    apply plugin: "signing"
    signing {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}
